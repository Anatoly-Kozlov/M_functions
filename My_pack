[
fxTableRemoveNullColumns = 
    [
    f1 = (tbl) =>
        [
        nms = Table.ColumnNames(tbl),
        null_cols = List.RemoveNulls( List.Transform( nms, (x)=> if List.NonNullCount(Table.Column(tbl,x)) = 0 then x 
                           else null )),
        to = Table.RemoveColumns(tbl,null_cols)
        ][to],
    typ=type function (tbl as table) as table meta 
        [Documentation.Name="fxTableRemoveNullColumns",
        Documentation.LongDescription="Функция удаляет пустые столбцы из таблицы"],
    result = Value.ReplaceType(f1,typ)
    ][result],

fxTableTransformColumn = 
    [
        f1 =(tbl,col_name,func) => 
            [
                a = Table.AddColumn(tbl,"tmp",func),
                b = Table.RemoveColumns (a,col_name),
                to = Table.RenameColumns(b,{"tmp",col_name})
            ][to],
        typ=type function (tbl as table, col_name as text, func as function) as table meta 
                    [Documentation.Name="fxTableTransformColumn",
                    Documentation.LongDescription="Функция трансформирует значения в столбце. При трансформировании можно обращаться к значениям других столбцов"],
        result = Value.ReplaceType(f1,typ)
    ][result]
]
